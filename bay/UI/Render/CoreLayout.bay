/*!
 *  Bayrell Core Library
 *
 *  (c) Copyright 2018-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Core.UI.Render;


use Runtime.CoreStruct;
use Core.UI.Assets;
use Core.UI.Render.CoreLayoutModel;
use Core.UI.Render.CoreView;
use Core.UI.Render.RenderHelper;


struct CoreLayout extends CoreView
{
	
	/**
	 * Required Assets
	 */
	lambda Collection<string> assets() =>
		[
			classof Assets,
		]
	;
	
	
	
	/**
	 * Required components
	 */
	lambda Collection<string> components() =>
		[
			
			
		]
	;
	
	
	
	/**
	 * Component css
	 */
	lambda string css(Dict<string> vars) => 
		@css{
			*{box-sizing: border-box;}body{margin:0;padding:0;}
		}
	;
	
	
	
	/**
	 * Render head
	 */
	lambda Template head(CoreLayoutModel data) =>
		return 
			<meta name='Content-Type' content='text/html; charset=utf-8'/>
			<title>@{ data.title }</title>
		;
	
	
	
	/**
	 * Render assets in header
	 */
	lambda Template assetsHeader(CoreLayoutModel data)
	{
		Vector<UIStruct> res = new Vector();
		
		/* Add assets */
		Collection<string> css = RenderHelper::loadSyncResources(data.assets);
		for (int i=0; i<css.count(); i++)
		{
			res.push(
				new UIStruct{
					"kind": UIStruct::TYPE_ELEMENT,
					"name": "link",
					"props": {
						"rel": "stylesheet",
						"href": css.item(i),
					},
				}
			);
		}
		
		/* Add components css */
		string css = RenderHelper::getCSSFromComponents(data.components, data.css_vars);
		res.push(
			new UIStruct{
				"kind": UIStruct::TYPE_ELEMENT,
				"name": "style",
				"props": {
					"type": "text/css",
				},
				"children": [
					rtl::normalizeUI(css),
				],
			}
		);
		
		return res.toCollection();
	}
	
	
	
	/**
	 * Render assets in body
	 */
	lambda Template assetsBody(CoreLayoutModel data)
	{
		Collection<string> js = RenderHelper::loadAsyncResources(data.assets);
		Vector<UIStruct> res = new Vector();
		for (int i=0; i<js.count(); i++)
		{
			res.push(
				new UIStruct{
					"kind": UIStruct::TYPE_ELEMENT,
					"name": "script",
					"props": {
						"src": js.item(i),
					},
				}
			);
		}
		res.push(
			new UIStruct{
				"kind": UIStruct::TYPE_ELEMENT,
				"name": "script",
				"props": {
					"src": "/assets/Core/UI/Drivers/RenderDriver.js",
				},
			}
		);
		return res.toCollection();
	}
	
	
	
	/**
	 * Content render
	 */
	lambda Template content(CoreLayoutModel data) => return data.content;
	
	
	
	/**
	 * Component render
	 */
	lambda Template render(CoreLayoutModel data) =>
		return <!DOCTYPE html>
			<html>
			<head>
				{ static::head( data ) }
				{ static::assetsHeader( data ) }
			</head>
			<body>
				<div id='root'>@raw{ static::content( data ) }</div>
				<input id='view' value={ data.view } style="display: none;" />
				<input id='model' value={ rtl::json_encode(data.model) } style="display: none;" />
				<div id='scripts'>
					{ static::assetsBody( data ) }
				</div>
			</body>
		</html>
	;
	
	
}